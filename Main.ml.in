module type SetDom = Interface.Domain
  with type sym = int
   and type cnstr = int LogicSymbolicSet.t
   and type output = int LogicSymbolicSet.t
   and type query = int LogicSymbolicSet.q

let domain : (module SetDom) list ref = ref []

let push (d : (module SetDom)) =
  domain := d :: !domain

let pop () =
  match !domain with
  | hd::rest ->
    domain := rest;
    hd
  | [] ->
    Printf.fprintf stderr "Error: domain combinator did not have a domain to use as input\n%!";
    exit 1

let arg_blank = ("", Arg.Unit (fun () -> ()), " ")

(** list of abstract domains to enable *)
let domains = [
#ifdef MLBDD
  ("-bdd-full", Arg.Unit (fun () -> push (module BDDFull.Domain)), " Full BDD-based domain");

  ("-bdd-opt", Arg.Unit (fun () -> push (module Eq.Domain.Make(Packer.Domain.Make(BDDFull.Domain)))), " Optimized BDD-based domain");
#endif
  ("-lin", Arg.Unit (fun () -> push (module Lin.Domain)), " Lin set domain");
#ifdef Z3
  ("-smt", Arg.Unit (fun () -> push (module SMT.Domain)), " SMT-based set domain");
#endif
#ifdef QBF
  ("-qbf", Arg.String (fun solver ->
      let s = match solver with
        | _ -> (module QBF.SolverDepQBF : QBF.Domain.Solver)
      in
      push (module QBF.Domain.Make((val s)))),
   "solver external QBF solver-based domain");
#endif
]

(** list of domain combinators to enable *)
let combinators = [

  (* logger combinator *)
  ("-logger", Arg.String (fun s ->
       let module L = (struct
         let file = s
       end) in
       let module D = (val pop ()) in
       let module Log = Logger.Domain.Make(L)(D) in
       push (module Log)
     ), "<file> Log domain interactions to file");

  (* packer combinator *)
  ("-pack", Arg.Unit (fun () ->
       let module D = (val pop ()) in
       let module P = Packer.Domain.Make(D) in
       push (module P)
     ), " Pack variables into multiple domain instances");

  (* equality combinator *)
  ("-eq", Arg.Unit (fun () ->
       let module D = (val pop ()) in
       let module P = Eq.Domain.Make(D) in
       push (module P)
     ), " Track equality constraints externally from the domain");

  (* debugger combinator *)
  ("-debug", Arg.Unit (fun () ->
       let module D = (val pop ()) in
       let module P = Debugger.Domain.Make(D) in
       push (module P)
     ), " Use debug printer");

  (* trace combinator *)
  ("-trace", Arg.String (fun s ->
       let c = s.[0] = '-' in
       let s = String.sub s 1 ((String.length s) - 1) in
       let module L = (struct
         let file = s
         let check = c
       end) in
       let module D = (val pop ()) in
       let module Log = Tracer.Domain.Make(L)(D) in
       push (module Log)
     ), "<file> Generate a strace file of domain operations");

  (* singleton handler *)
  ("-sing", Arg.Unit (fun () ->
       let module D = (val pop ()) in
       let module P = Sing.Domain.Make(D) in
       push (module P)
     ), " Support singleton sets");

  (* statistics *)
  ("-stats", Arg.Unit (fun () ->
       let module D = (val pop ()) in
       let module P = Stats.Domain.Make(D) in
       push (module P)
     ), " Accumulate statistics on domain operations");
]

let usage =
  "Usage: sdsl <domain config> [file]\n" ^
  "  <domain config> indicates which domain should be run.  Domains can be\n" ^
  "  specified as below.  Each operation manipulates a stack of domains.\n" ^
  "  Basic domains, push a new domain onto the stack.  Combinators consume\n" ^
  "  domains off the stack and produce new domains.  At least one domain must\n" ^
  "  be specified.  Extra domains on the stack are ignored and will issue a\n" ^
  "  warning.\n"

type ft =
  | FT_SDSL
  | FT_STRACE


let stream = ref ((fun () -> ()),stdin,FT_SDSL)

let get_stream fname =
  let l = String.length fname in
  let ft = if (String.sub fname (l-7) 7) = ".strace" then
      FT_STRACE
    else
      FT_SDSL
  in
  let fin = open_in fname in
  stream := ((fun () -> close_in fin), fin, ft)



let run () =
  (* assemble command line arguments *)
  let args = 
    domains @
    [arg_blank] @
    combinators @
    [arg_blank] @
    SDSL.Interp.args @
    [arg_blank]
  in
  let args = Arg.align args in

  begin try
      (* parse environment args *)
      let envargv = try Sys.getenv "SDSLPARAMS" |>
                        Str.split (Str.regexp " +") |>
                        Array.of_list
        with Not_found -> [| |] in

      (* add environment args to command line arguments *)
      let argv = Array.append Sys.argv envargv in

      (* parse arguments *)
      Arg.current := 0;
      Arg.parse_argv argv args get_stream usage;
    with
    | Arg.Help s ->
      Printf.fprintf stderr "%s\n%!" s;
      exit 1
    | Arg.Bad s ->
      Printf.fprintf stderr "%s\n\n%s%!" s (Arg.usage_string args usage);
      exit 1
  end;
    
  (* build abstract interpreter *)
  let d : (module SetDom) = match !domain with
    | [d] -> d
    | d::_ ->
      Printf.fprintf stderr "Warning: Unused domain(s) on domain stack\n%!";
      d
    | [] ->
      Printf.fprintf stderr "Error: Domain stack is empty, please specify an abstract domain\n\n%s%!" (Arg.usage_string args usage);
      exit 1
  in
  let module D = (val d) in
  let module I = SDSL.Interp.Make(D) in
  let module IT = STrace.Interp.Make(D) in

  (* do parsing *)
  let (close,stream,ft) = !stream in
  let lexbuf = Lexing.from_channel stream in
  begin match ft with
  | FT_SDSL ->
    let ast = try
        SDSL.Parser.program SDSL.Lexer.token lexbuf
      with Parsing.Parse_error ->
        let pos = lexbuf.Lexing.lex_curr_p in
        Format.printf "(%d:%d) Parse error@."
          pos.Lexing.pos_lnum (pos.Lexing.pos_cnum - pos.Lexing.pos_bol);
        exit 1
    in

    (* run the analysis *)
    I.interpret ast;
  | FT_STRACE ->
    let ast = try
        STrace.Parser.trace STrace.Lexer.token lexbuf
      with Parsing.Parse_error ->
        let pos = lexbuf.Lexing.lex_curr_p in
        Format.printf "(%d:%d) Parse error@."
          pos.Lexing.pos_lnum (pos.Lexing.pos_cnum - pos.Lexing.pos_bol);
        exit 1
    in

    (* run the analysis *)
    IT.interpret ast

  end;

  (* clean up *)
  close ()



let _ =
  run ()
